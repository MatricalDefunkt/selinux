# Maintainer: Nicolas Iooss (nicolas <dot> iooss <at> m4x <dot> org)
# Contributor: Timoth√©e Ravier <tim@siosm.fr>
# Contributor: Nicky726 (Nicky726 <at> gmail <dot> com)
# Contributor: Sergej Pupykin (pupykin <dot> s+arch <at> gmail <dot> com)
#
# This PKGBUILD is maintained on https://github.com/archlinuxhardened/selinux.
# If you want to help keep it up to date, please open a Pull Request there.

pkgname=libsepol
_pkgver=3.0-rc1
pkgver=${_pkgver/-/}
pkgrel=2
pkgdesc="SELinux binary policy manipulation library"
arch=('i686' 'x86_64' 'armv6h')
url='http://userspace.selinuxproject.org'
license=('LGPL2.1')
groups=('selinux')
makedepends=('flex')
depends=('glibc')
options=(staticlibs)
conflicts=("selinux-usr-${pkgname}")
provides=("selinux-usr-${pkgname}=${pkgver}-${pkgrel}")
source=("https://github.com/SELinuxProject/selinux/releases/download/20191031/${pkgname}-${_pkgver}.tar.gz"
        '0001-libsepol-cil-Report-disabling-an-optional-block-only.patch')
sha256sums=('94240759783744efde424d884e3de8ffb7375a35b854497069fa053eb6f23fd1'
            '89cb525d6345117bf1b0454a4ac269e06b9c3249b63955654740d8b5bbb4f4ff')

prepare() {
  cd "${pkgname}-${_pkgver}"
  patch -Np2 -i ../0001-libsepol-cil-Report-disabling-an-optional-block-only.patch
}

build() {
  cd "${pkgname}-${_pkgver}"
  make
}

package() {
  cd "${pkgname}-${_pkgver}"
  make DESTDIR="${pkgdir}" SHLIBDIR=/usr/lib install
}
