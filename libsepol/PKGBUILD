# Maintainer: Nicolas Iooss (nicolas <dot> iooss <at> m4x <dot> org)
# Contributor: Timoth√©e Ravier <tim@siosm.fr>
# Contributor: Nicky726 (Nicky726 <at> gmail <dot> com)
# Contributor: Sergej Pupykin (pupykin <dot> s+arch <at> gmail <dot> com)
#
# This PKGBUILD is maintained on https://github.com/archlinuxhardened/selinux.
# If you want to help keep it up to date, please open a Pull Request there.

pkgname=libsepol
_pkgver=3.2-rc3
pkgver=${_pkgver/-/}
pkgrel=1
pkgdesc="SELinux binary policy manipulation library"
arch=('i686' 'x86_64' 'armv6h')
url='https://github.com/SELinuxProject/selinux'
license=('LGPL2.1')
groups=('selinux')
makedepends=('flex')
depends=('glibc')
options=(staticlibs)
conflicts=("selinux-usr-${pkgname}")
provides=("selinux-usr-${pkgname}=${pkgver}-${pkgrel}")
source=("https://github.com/SELinuxProject/selinux/releases/download/${_pkgver}/${pkgname}-${_pkgver}.tar.gz")
sha256sums=('36ce63e4782b6685a2d1415ebff5bac9831d4bb7fee685003b2b51e3921550aa')

build() {
  cd "${pkgname}-${_pkgver}"

  export CFLAGS="${CFLAGS} -fno-semantic-interposition"
  make

  # Build a libsepol.so.1 to ease the transition from libsepol 3.1 to 3.2
  make -C src LIBVERSION=1 libsepol.so.1
  rm src/libsepol.so
}

package() {
  cd "${pkgname}-${_pkgver}"
  make DESTDIR="${pkgdir}" SHLIBDIR=/usr/lib install

  install -Dm755 src/libsepol.so.1 "${pkgdir}/usr/lib"
}
