# Maintainer: Nicolas Iooss (nicolas <dot> iooss <at> m4x <dot> org)

pkgbase=selinux-python
pkgname=(selinux-python selinux-python2)
_pkgver=2.7-rc2
pkgver=${_pkgver/-/}
pkgrel=1
pkgdesc="SELinux python tools and libraries"
groups=('selinux')
arch=('i686' 'x86_64')
url='https://github.com/SELinuxProject/selinux/wiki'
license=('GPL2')
makedepends=('python2' 'python' 'python-ipy' 'libsemanage>=2.7rc1' 'setools>=4.0.0')
conflicts=('sepolgen<2.7rc1' 'policycoreutils<2.7rc1')
provides=("sepolgen=${pkgver}-${pkgrel}")
source=("https://raw.githubusercontent.com/wiki/SELinuxProject/selinux/files/releases/20170616/${pkgname}-${_pkgver}.tar.gz")
sha256sums=('c8afc3e6ba05f95dbb1546105287f4bb7a1e8563b2f6b39cb440baf5ca70448a')

build() {
  cd "${pkgbase}-${_pkgver}"
  make PYTHON=/usr/bin/python2
  make PYTHON=/usr/bin/python3
}

package_selinux-python() {
  depends=('python' 'python-ipy' 'libsemanage>=2.7rc1' 'setools>=4.0.0')

  cd "${pkgbase}-${_pkgver}"
  make PYTHON=/usr/bin/python3 DESTDIR="${pkgdir}" \
    LIBSEPOLA=/usr/lib/libsepol.a \
    SBINDIR="${pkgdir}/usr/bin" \
    USRSBINDIR="${pkgdir}/usr/bin" \
    install
  /usr/bin/python3 -m compileall "${pkgdir}/$(/usr/bin/python3 -c 'import site; print(site.getsitepackages()[0])')"
}

package_selinux-python2() {
  depends=('python2' 'libsemanage>=2.7rc1' 'setools>=4.0.0')

  cd "${pkgbase}-${_pkgver}"
  # Guide the files which would conflict with selinux-python package into a
  # special directory, which would then be removed
  make PYTHON=/usr/bin/python2 DESTDIR="${pkgdir}" \
    BASHCOMPLETIONDIR="${pkgdir}/removed" \
    BINDIR="${pkgdir}/removed" \
    LIBDIR="${pkgdir}/removed" \
    LIBSEPOLA=/usr/lib/libsepol.a \
    MANDIR="${pkgdir}/removed" \
    SBINDIR="${pkgdir}/removed" \
    SHAREDIR="${pkgdir}/removed" \
    USRSBINDIR="${pkgdir}/removed" \
    install
  rm -r "${pkgdir}/removed"
  /usr/bin/python2 -m compileall "${pkgdir}/$(/usr/bin/python2 -c 'import site; print(site.getsitepackages()[0])')"
}
